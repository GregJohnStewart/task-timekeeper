plugins {
    id 'java'
    id "io.freefair.lombok" version "4.1.5"
    id 'io.quarkus'
}

repositories {
     mavenLocal()
     mavenCentral()
    jcenter()
}

dependencies {
    compile project(":BaseCode:Core")
    compile project(":BaseCode:ManagerIO")
    compile project(":BaseCode:Stats")
    compile project(":WebServer:WebLibrary")

    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-config-yaml'
    implementation 'io.quarkus:quarkus-mailer'
    implementation 'io.quarkus:quarkus-resteasy-jackson'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-scheduler'
    // https://quarkus.io/guides/mongodb
    // https://quarkus.io/guides/mongodb-panache
    implementation 'io.quarkus:quarkus-mongodb-panache'
    implementation 'io.quarkus:quarkus-resteasy'
    // https://quarkus.io/guides/security-properties
//    implementation 'io.quarkus:quarkus-elytron-security-properties-file'
//    implementation 'io.quarkus:quarkus-elytron-security'
    // https://quarkus.io/guides/security-jwt 
    // https://vertx.io/docs/vertx-auth-jwt/java/
//    implementation 'io.quarkus:quarkus-smallrye-jwt'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
    nativeTestImplementation 'io.quarkus:quarkus-junit5'
    nativeTestImplementation 'io.rest-assured:rest-assured'
    nativeTestImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
}

group 'com.gjs.taskTimekeeper.webServer'
version = '1.0.0.216'
String projectPropertiesFileName = "application.yml";
String openApiConfigFileName = "META-INF/" + "openapi.yml"
String projectResourcesDir = "$projectDir/src/main/resources/";

task writeOpenApiFile() {
    doLast {
        new File(projectResourcesDir + openApiConfigFileName).text = """
#################################################################################
#                                                                               #
# Open API base information.                                                    #
#   Template api details to be merged with the ones generated from annotations. #
#                                                                               #
# AUTOGENERATED BY GRADLE AT BUILD TIME. DO NOT EDIT THIS FILE                  #
#                                                                               #
#################################################################################

openapi: 3.0.1
info:
  title: "Task Timekeeper WebServer API"
  version: "$version"
  contact:
    name: "Greg Stewart"
    url: "https://gjstewart.net"
    email: "contact@gjstewart.net"
paths:
"""
    }
}
task writePropertiesFile() {
    doLast {
        new File(projectResourcesDir + projectPropertiesFileName).text = """
################################################################
#                                                              #
# Main properties file. Contains values used in the server.    #
#                                                              #
# AUTOGENERATED BY GRADLE AT BUILD TIME. DO NOT EDIT THIS FILE #
#                                                              #
################################################################

version: $version
buildtime: ${ZonedDateTime.now().toString()}
# Versions of the libraries used
lib:
  base:
    core:
      version: ${project(":BaseCode:Core").version}
    managerIO:
      version: ${project(":BaseCode:ManagerIO").version}
    stats:
      version: ${project(":BaseCode:Stats").version}
  server:
    webLibrary:
      version: ${project(":WebServer:WebLibrary").version}

quarkus:
  mongodb:
    database: task-timekeeper
    application-name: task-timekeeper-server
"""
    }
}

task deleteGenertatedResources(type: Delete) {
    delete fileTree(projectResourcesDir) {
        include "**/$openApiConfigFileName"
        include "**/$projectPropertiesFileName"
    }
}

clean {
    dependsOn 'deleteGenertatedResources';
}

compileJava {
    options.compilerArgs << '-parameters'
    println "WebServer version at compile time: " + version
    dependsOn 'writePropertiesFile';
    dependsOn 'writeOpenApiFile';

    dependsOn ':BaseCode:Core:test'
    dependsOn ':BaseCode:ManagerIO:test'
    dependsOn ':BaseCode:Stats:test'
    dependsOn ':WebServer:WebLibrary:test'
    dependsOn 'incrementBuildVersion';
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// stop lombok from overwriting its own config
generateLombokConfig.enabled = false
